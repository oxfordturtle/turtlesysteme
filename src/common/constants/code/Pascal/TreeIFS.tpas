PROGRAM TreeIFS;
CONST xleft = 0;
      xright = 999;
      ytop = 0;
      ybottom = 999;
      scaledown = 4;
      mappings = 5;
      title = 'Tree';
      live = green;
      copied = emerald;
      pauseval = 2000;
VAR xl, xr, yt, yb: integer;
    coeffdiv: integer;
    mapxx, mapxy, mapxc: array[1..mappings] of integer;
    mapyx, mapyy, mapyc: array[1..mappings] of integer;
    iteration, starttime: integer;
    count, numborn, numkilled: integer;

  Procedure setmaps;
  Begin
    coeffdiv := 1000;
    mapxx[1] := 195;
    mapyx[1] := 344;
    mapxy[1] := -488;
    mapyy[1] := 443;
    mapxc[1] := 443;
    mapyc[1] := 245;
    mapxx[2] := 462;
    mapyx[2] := -252;
    mapxy[2] := 414;
    mapyy[2] := 361;
    mapxc[2] := 251;
    mapyc[2] := 569;
    mapxx[3] := -58;
    mapyx[3] := 453;
    mapxy[3] := -70;
    mapyy[3] := -111;
    mapxc[3] := 598;
    mapyc[3] := 97;
    mapxx[4] := -35;
    mapyx[4] := -469;
    mapxy[4] := 70;
    mapyy[4] := -22;
    mapxc[4] := 488;
    mapyc[4] := 507;
    mapxx[5] := -637;
    mapyx[5] := 0;
    mapxy[5] := 0;
    mapyy[5] := 501;
    mapxc[5] := 856;
    mapyc[5] := 251
  End;

  Procedure setscale;
  Var i: integer;
  Begin
    xl := xleft / scaledown;
    xr := xright / scaledown;
    yt := ytop / scaledown;
    yb := ybottom / scaledown;
    for i := 1 to mappings do
      begin
        mapxc[i] := mapxc[i] / scaledown;
        mapyc[i] := mapyc[i] / scaledown
      end
  End;

  Procedure map(a, x, y: integer; var newx, newy: integer);
  Begin
    newx := divmult(x, coeffdiv, mapxx[a]) + divmult(y, coeffdiv, mapxy[a]) + mapxc[a];
    newy := divmult(x, coeffdiv, mapyx[a]) + divmult(y, coeffdiv, mapyy[a]) + mapyc[a]
  End;

  Procedure domap(mapnum: integer);
  Var x, y, newx, newy: integer;
  Begin
    noupdate;
    for y := yt to yb do
      for x := xl to xr do
        if pixcol(x, y) <> white then
          begin
            map(mapnum, x, y, newx, newy);
            if pixcol(newx, newy) = white then
              inc(numborn);
            pixset(newx, newy, copied)
          end;
    update
  End;

  Procedure cleanup;
  Var x, y: integer;
  Begin
    noupdate;
    for y := yt to yb do
      for x := xl to xr do
        if pixcol(x, y) = live then
          begin
            pixset(x, y, white);
            inc(numkilled)
          end
        else
        if pixcol(x, y) = copied then
          pixset(x, y, live);
    update
  End;

BEGIN
  setmaps;
  setscale;
  canvas(xl, yt, xr - xl + 1, yb - yt + 1);
  resolution(xr - xl + 1, yb - yt + 1);
  pixset(1, 1, live);
  iteration := 0;
  writeln(title + ' by iterative mapping');
  timeset(0);
  repeat
    starttime := time;
    inc(iteration);
    writeln('');
    writeln('Iteration ' + str(iteration) + ':');
    numborn := 0;
    for count := 1 to mappings do
      domap(count);
    numkilled := 0;
    cleanup;
    writeln('  ' + str(numborn) + ' pixels born; ' + str(numkilled) + ' pixels killed');
    writeln('  (took ' + str((time - starttime) / 1000) + ' seconds - total ' + str(time / 1000) + ' seconds so far)');
    if (numborn + numkilled > 0) and (pauseval >= 1000) then
      begin
        write('  (now pausing ' + qstr(pauseval, 1000, 1) + ' seconds to allow halting if desired ...)');
        pause(pauseval);
        writeln('')
      end
  until numborn + numkilled = 0;
  writeln('FINISH - fixed point of iterative mapping has been reached')
END.
