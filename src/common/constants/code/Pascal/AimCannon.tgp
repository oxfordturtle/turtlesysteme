PROGRAM AimCannon;
CONST gunlength = 1000;
      distancecol = green;
      timecol = red;
VAR n: integer;
    steps: integer;

  Procedure graphaxes;
  Var n: integer;
  Begin
    thickness(5);
    setxy(9700, 600);
    drawxy(0, 3900);
    drawxy(-9000, 0);
    drawxy(0, -3900);
    for n := 0 to 90 do
      if n mod 5 = 0 then
        begin
          setxy(650 + n * 100, 4600);
          print(str(n), 2, 16)
        end;
    for n := 0 to 9 do
      begin
        setxy(100, 4350 - n * 400);
        print(str(n * 1000), 2, 16)
      end;
    for n := 0 to 9 do
      begin
        setxy(9900, 4380 - n * 400);
        print(str(n * 20), 2, 16)
      end;
    setxy(4000, 4900);
    print('Angle of elevation', 2, 24);
    colour(distancecol);
    setxy(0, 100);
    print('Distance', 2, 24);
    colour(timecol);
    setxy(9600, 100);
    print('Time', 2, 24)
  End;

  Procedure plot(x, y, col: integer);
  Begin
    setxy(700 + x * 100, 4500 - y * 2 / 5);
    colour(col);
    blot(40)
  End;

  Procedure aim;
  Begin
    repeat
      noupdate;
      setxy(0, 10000);
      colour(cream);
      blot(1600);
      colour(yellowgreen);
      blot(1400);
      colour(maroon);
      setxy(250, 9750);
      blot(250);
      forward(gunlength);
      setxy(250, 9750);
      colour(red);
      blot(100);
      update;
      repeat
      until (?lmouse > 0);
      if pixcol(?mousex, ?mousey) = cream then
        turnxy(?mousex - 250, ?mousey - 9750)
    until pixcol(?mousex, ?mousey) = red
  End;

  Procedure fire(xvel, yvel, gravity, floor: integer);
  Begin
    setxy(250, 9750);
    steps := 0;
    repeat
      inc(steps);
      if pixcol(turtx, turty) = black then
        begin
          colour(white);
          blot(90)
        end;
      movexy(xvel, yvel);
      yvel := yvel + gravity;
      if turty > floor then
        turty := floor;
      if (pixcol(turtx, turty) = white) or (turty = floor) then
        begin
          colour(black);
          blot(75)
        end;
      update;
      pause(10);
      noupdate
    until turty = floor
  End;

BEGIN
  canvas(0, 0, 10500, 10000);
  graphaxes;
  turtd := 45;
  thickness(20);
  repeat
    aim;
    write('Elevation: ' + str(90 - turtd));
    fire(cos(90 - turtd, 1, 96), sin(90 - turtd, 1, -96), 1, 9950);
    write('  Distance: ' + str(turtx));
    writeln('  Time: ' + str(steps));
    plot(90 - turtd, turtx, distancecol);
    plot(90 - turtd, steps * 50, timecol)
  until 0 = 1
END.
