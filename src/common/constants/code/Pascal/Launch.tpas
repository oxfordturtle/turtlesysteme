PROGRAM Launch;
CONST earthradius = 6371000;
      earthgm = 398600442;
      initdirection = 2000;
      initthrust = 20000;
      thrusttime = 500;
      angleprecision = 1296000;
VAR x, y, d, xvel, yvel: integer;
    thrust, xthrust, ythrust: integer;
    gravity, xgravity, ygravity: integer;
    xacc, yacc: integer;
    dist, prevdist, prevdiff, t: integer;
    explosion: integer;

  Procedure drawrocket;
  Begin
    noupdate;
    blank(black);
    colour(teal);
    setxy(0, 0);
    blot(earthradius / 1000);
    setxy(x / 1000, y / 1000);
    direction(d);
    thickness(4);
    colour(silver);
    forward(200);
    back(200);
    if thrust > 0 then
      begin
        thickness(8);
        colour(yellow);
        back(200);
        forward(200)
      end;
    update
  End;

  Procedure report;
  Begin
    writeln('');
    writeln('TIME = ' + str(t));
    writeln(' Xposition = ' + str(x) + ' metres');
    writeln(' Yposition = ' + str(y) + ' metres');
    writeln(' Height = ' + qstr(dist - earthradius, 1000, 2) + ' km');
    writeln(' Direction = ' + qstr(d, 3600, 1) + ' degrees');
    writeln(' Thrust = ' + qstr(thrust, 1000, 1) + ' newtons per kg');
    writeln('  Xthrust = ' + qstr(xthrust, 1000, 1) + ' newtons per kg');
    writeln('  Ythrust = ' + qstr(ythrust, 1000, 1) + ' newtons per kg');
    writeln(' Gravity = ' + qstr(gravity, 1000, 1) + ' newtons per kg');
    writeln('  Xgravity = ' + qstr(xgravity, 1000, 1) + ' newtons per kg');
    writeln('  Ygravity = ' + qstr(ygravity, 1000, 1) + ' newtons per kg');
    writeln(' Xvelocity = ' + qstr(xvel, 1000, 1) + ' metres per s');
    writeln(' Yvelocity = ' + qstr(yvel, 1000, 1) + ' metres per s')
  End;

BEGIN
  canvas(-20000, -20000, 40000, 40000);
  angles(angleprecision);
  x := 0;
  y := -earthradius;
  xvel := 0;
  yvel := 0;
  d := initdirection;
  t := 0;
  prevdiff := 1;
  thrust := initthrust;
  repeat
    drawrocket;
    prevdist := dist;
    dist := hypot(x, y ,1);
    if (dist - prevdist) * prevdiff <= 0 then
      report;
    prevdiff := dist - prevdist;
    gravity := divmult(earthgm, divmult(dist, 1000000, dist), 1000);
    xgravity := divmult(gravity, dist, -x);
    ygravity := divmult(gravity, dist, -y);
    xthrust := sin(d, 1, thrust);
    ythrust := -cos(d, 1, thrust);
    xvel := xvel + xgravity + xthrust;
    yvel := yvel + ygravity + ythrust;
    x := x + xvel / 1000;
    y := y + yvel / 1000;
    if yvel > 0 then
      d := angleprecision / 2 - arctan(xvel, yvel, 1)
    else
      d := arctan(xvel, -yvel, 1);
    inc(t);
    if t = thrusttime then
      thrust := 0
  until (0 = 1) or (dist < earthradius) or (dist > 40000000);
  if dist < earthradius then
    begin
      colour(yellow);
      for explosion := 100 to 200 do
        begin
          blot(explosion);
          pause(3)
        end;
      colour(black);
      blot(200)
    end
END.
