PROGRAM SpongeThrow;
CONST facesize = 100;
      spongesize = 40;
VAR x, y: integer;
    xvel, yvel: integer;

  Procedure face(size: integer);

    procedure eye;
    begin
      colour(white);
      ellblot(size * 3 / 20, size / 10);
      colour(emerald);
      blot(size * 9 / 100);
      colour(black);
      blot(size / 25)
    end;

  Begin
    direction(0);
    colour(peach);
    blot(size);
    colour(darkred);
    blot(size * 7 / 10);
    forward(size * 3 / 20);
    colour(peach);
    blot(size * 3 / 4);
    back(size * 3 / 10);
    colour(royal);
    blot(size * 3 / 20);
    penup;
    forward(size * 9 / 20);
    left(90);
    forward(size * 2 / 5);
    eye;
    back(size * 4 / 5);
    eye
  End;

BEGIN
  x := 300;
  y := 700;
  xvel := 8;
  yvel := -4;
  while 0 < 1 do
    begin
      noupdate;
      colour(white);
      setxy(x, y);
      blot(facesize + spongesize);
      x := x + xvel;
      y := y + yvel;
      setxy(x, y);
      face(facesize);
      update;
      pause(10);
      if (?lmouse > 0) and (hypot(?mousex - x, ?mousey - y, 1) < facesize) then
        begin
          setxy(?mousex, ?mousey);
          randcol(4);
          blot(spongesize);
          pause(100)
        end;
      if (x < facesize) or (x > 999 - facesize) then
        xvel := -xvel;
      if (y < facesize) or (y > 999 - facesize) then
        yvel := -yvel
    end
END.
