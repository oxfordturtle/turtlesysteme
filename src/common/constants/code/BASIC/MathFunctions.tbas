REM MathFunctions

CONST DECIMALS% = 4
CONST DENOMINATOR% = 1000000
OUTPUT(TRUE, WHITE, TRUE)
WRITELN("This program selects two random numbers a and b, in the")
WRITELN("range 100-999, and then outputs various mathematical")
WRITELN("functions of the fraction a/b. The results are expressed")
WRITELN("to " + STR$(DECIMALS%) + " decimal places, and a denominator of " + STR$(DENOMINATOR%) + " is")
WRITELN("used in the calculations to provide precision, even")
WRITELN("though the Turtle functions strictly operate only on")
WRITELN("integers and produce integer results.")
WRITELN("")
PROCselectAB
WRITELN("a = " + STR$(A%) + "; b = " + STR$(B%))
WRITELN("a / b = " + STR$(A%) + " / " + STR$(B%) + " = " + QSTR$(A%, B%, DECIMALS%))
WRITELN("")
PROCshow("square", POWER(A%, B%, 2, DENOMINATOR%))
PROCshow("square root", ROOT(A%, B%, 2, DENOMINATOR%))
PROCshow("cube", POWER(A%, B%, 3, DENOMINATOR%))
PROCshow("cube root", ROOT(A%, B%, 3, DENOMINATOR%))
WRITELN("")
PROCshow("hypot", HYPOT(A%, B%, DENOMINATOR%))
PROCshow("calculated hypot", SQR(A% * A% + B% * B%, DENOMINATOR%))
WRITELN("")
PROCshow("sin", SIN(A%, B%, DENOMINATOR%))
PROCshow("cos", COS(A%, B%, DENOMINATOR%))
PROCshow("tan", TAN(A%, B%, DENOMINATOR%))
WRITELN("")
PROCshow("ln", LN(A%, B%, DENOMINATOR%))
IF DIVMULT(A%, B%, 1000) + LN(DENOMINATOR%, 1, 1000) > LN(MAXINT, 1, 1000) THEN
  WRITELN("EXP(" + STR$(A%) + " / " + STR$(B%) + ") would give numerical overflow, because")
  WRITELN("the result * " + STR$(DENOMINATOR%) + " would be greater than " + STR$(MAXINT))
ELSE
  PROCshow("exp", EXP(A%, B%, DENOMINATOR%))
ENDIF
PROCshow("log10", LOG10(A%, B%, DENOMINATOR%))
IF DIVMULT(A%, B%, 1000) + LOG10(DENOMINATOR%, 1, 1000) > LOG10(MAXINT, 1, 1000) THEN
  WRITELN("ANTILOG(" + STR$(A%) + " / " + STR$(B%) + ") would give numerical overflow, because")
  WRITELN("the result * " + STR$(DENOMINATOR%) + " would be greater than " + STR$(MAXINT))
ELSE
  PROCshow("antilog", ANTILOG(A%, B%, DENOMINATOR%))
ENDIF
WRITELN("")
IF A% > B% THEN
  WRITELN("ACS and ASN are not defined for " + STR$(A%) + " / " + STR$(B%) + " = " + QSTR$(A%, B%, DECIMALS%))
ELSE
  PROCshow("arccos", ACS(A%, B%, DENOMINATOR%))
  PROCshow("arcsin", ASN(A%, B%, DENOMINATOR%))
ENDIF
PROCshow("arctan", ATN(A%, B%, DENOMINATOR%))
END

DEF PROCshow(s$, n%)
  WRITELN(s$ + "(" + STR$(A%) + " / " + STR$(B%) + ") = " + QSTR$(n%, DENOMINATOR%, DECIMALS%))
ENDPROC

DEF PROCselectAB
  A% = RND(900) + 99
  B% = RND(900) + 99
ENDPROC
