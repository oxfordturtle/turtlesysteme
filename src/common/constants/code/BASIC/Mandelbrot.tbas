REM Mandelbrot

CONST MAXCOL% = 40
CONST SCALE% = 250
CONST PIXELS% = 750
CONST XCENTRE% = -500000
CONST YCENTRE% = 0
xstart% = DIVMULT(XCENTRE%, 1000000, SCALE%) - PIXELS% / 2
ystart% = DIVMULT(YCENTRE%, 1000000, SCALE%) - PIXELS% / 2
xfinish% = xstart% + PIXELS% - 1
yfinish% = ystart% + PIXELS% - 1
CANVAS(xstart%, ystart%, PIXELS%, PIXELS%)
RESOLUTION(PIXELS%, PIXELS%)
TIMESET(0)
FOR a% = xstart% TO xfinish%
  NOUPDATE
  FOR b% = ystart% TO yfinish%
    x% = a%
    y% = b%
    iterations% = 0
    WHILE (HYPOT(x%, y%, 1) < 2 * SCALE%) AND (iterations% <= MAXCOL%)
      temp% = DIVMULT(x% + y%, SCALE%, x% - y%)
      y% = DIVMULT(2 * x%, SCALE%, y%) + b%
      x% = temp% + a%
      INC(iterations%)
    ENDWHILE
    IF iterations% > MAXCOL% THEN
      PIXSET(a%, b%, BLACK)
    ELSE
      PIXSET(a%, b%, RGB(iterations% + 1))
    ENDIF
  NEXT
  UPDATE
NEXT
WRITELN("Resolution: " + STR$(SCALE% * 3) + " Time taken: " + QSTR$(TIME, 1000, 1) + " seconds.")
END
