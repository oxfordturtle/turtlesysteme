REM IFSColour

CONST MAXMAPPINGS% = 4
DIM mapxx%(MAXMAPPINGS%)
DIM mapxy%(MAXMAPPINGS%)
DIM mapxc%(MAXMAPPINGS%)
DIM mapyx%(MAXMAPPINGS%)
DIM mapyy%(MAXMAPPINGS%)
DIM mapyc%(MAXMAPPINGS%)
PROCstartprompt
PROCsetscale
CANVAS(xl%, yt%, xr% - xl% + 1, yb% - yt% + 1)
RESOLUTION(xr% - xl% + 1, yb% - yt% + 1)
PROCbackground
FOR count% = 1 TO mappings%
  PROCdomap(count%)
NEXT
END

DEF PROCsetsierpinski
  xleft% = 0
  xright% = 999
  ytop% = 0
  ybottom% = 999
  scaledown% = 2
  mappings% = 3
  gridscale% = 5
  gridthick% = 64
  coeffdiv% = 100
  mapxx%(1) = 50
  mapyx%(1) = 0
  mapxy%(1) = 0
  mapyy%(1) = 50
  mapxc%(1) = 250
  mapyc%(1) = 0
  mapxx%(2) = 50
  mapyx%(2) = 0
  mapxy%(2) = 0
  mapyy%(2) = 50
  mapxc%(2) = 0
  mapyc%(2) = 500
  mapxx%(3) = 50
  mapyx%(3) = 0
  mapxy%(3) = 0
  mapyy%(3) = 50
  mapxc%(3) = 500
  mapyc%(3) = 500
ENDPROC

DEF PROCsetbarnsley
  xleft% = -236
  xright% = 263
  ytop% = 1
  ybottom% = 1000
  scaledown% = 2
  mappings% = 4
  gridscale% = 5
  gridthick% = 32
  coeffdiv% = 100
  mapxx%(1) = 0
  mapyx%(1) = 0
  mapxy%(1) = 0
  mapyy%(1) = 16
  mapxc%(1) = 0
  mapyc%(1) = 0
  mapxx%(2) = 85
  mapyx%(2) = -4
  mapxy%(2) = 4
  mapyy%(2) = 85
  mapxc%(2) = 0
  mapyc%(2) = 160
  mapxx%(3) = 20
  mapyx%(3) = 23
  mapxy%(3) = -26
  mapyy%(3) = 22
  mapxc%(3) = 0
  mapyc%(3) = 160
  mapxx%(4) = -15
  mapyx%(4) = 26
  mapxy%(4) = 28
  mapyy%(4) = 24
  mapxc%(4) = 0
  mapyc%(4) = 44
ENDPROC

DEF PROCsetdragon
  xleft% = 0
  xright% = 999
  ytop% = 0
  ybottom% = 999
  scaledown% = 4
  mappings% = 3
  gridscale% = 5
  gridthick% = 64
  coeffdiv% = 1000
  mapxx%(1) = 0
  mapyx%(1) = -577
  mapxy%(1) = 577
  mapyy%(1) = 0
  mapxc%(1) = 95
  mapyc%(1) = 589
  mapxx%(2) = 0
  mapyx%(2) = -577
  mapxy%(2) = 577
  mapyy%(2) = 0
  mapxc%(2) = 441
  mapyc%(2) = 789
  mapxx%(3) = 0
  mapyx%(3) = -577
  mapxy%(3) = 577
  mapyy%(3) = 0
  mapxc%(3) = 95
  mapyc%(3) = 989
ENDPROC

DEF PROCstartprompt
  OUTPUT(TRUE, CREAM, TRUE)
  KEYECHO(FALSE)
  WRITELN("ITERATED FUNCTIONS ON COLOURED BACKGROUND")
  WRITELN("")
  WRITE("Select Sierpinski, Barnsley fern, or Dragon curve? (S/B/D) ")
  KEYECHO(FALSE)
  REPEAT
  UNTIL (ABS(?KEY) = \s) OR (ABS(?KEY) = \b) OR (ABS(?KEY) = \d)
  WRITELN(CHR$(ABS(?KEY)))
  IF ABS(?KEY) = \s THEN
    WRITELN("Sierpinski mappings will be used")
    PROCsetsierpinski
  ELSE
    IF ABS(?KEY) = \b THEN
      WRITELN("Barnsley fern mappings will be used")
      PROCsetbarnsley
    ELSE
      WRITELN("Dragon curve mappings will be used")
      PROCsetdragon
    ENDIF
  ENDIF
  OUTPUT(FALSE, WHITE, FALSE)
ENDPROC

DEF PROCsetscale
  xl% = xleft% / scaledown%
  xr% = xright% / scaledown%
  yt% = ytop% / scaledown%
  yb% = ybottom% / scaledown%
  FOR i% = 1 TO mappings%
    mapxc%(i%) = mapxc%(i%) / scaledown%
    mapyc%(i%) = mapyc%(i%) / scaledown%
  NEXT
  gt% = gridthick% / scaledown%
ENDPROC

DEF PROCbackground
  LOCAL x%, y%
  LOCAL cx%, cy%, r%, g%, b%
  THICKNESS(gt%)
  COLOUR(DARKGREY)
  FOR x% = 1 TO gridscale% - 1
    SETXY(xl% + DIVMULT(xr% - xl%, gridscale%, x%), yt%)
    DRAWXY(0, yb% - yt%)
  NEXT
  FOR y% = 1 TO gridscale% - 1
    SETXY(xl% + gt% / 2, yt% + DIVMULT(yb% - yt%, gridscale%, y%))
    DRAWXY(xr% - xl% - gt%, 0)
  NEXT
  COLOUR(BLACK)
  SETXY(xl%, yt%)
  DRAWXY(xr% - xl%, 0)
  DRAWXY(0, yb% - yt%)
  DRAWXY(xl% - xr%, 0)
  DRAWXY(0, yt% - yb%)
  FOR x% = 0 TO gridscale% - 1
    FOR y% = 0 TO gridscale% - 1
      cx% = xl% + DIVMULT(xr% - xl%, gridscale%, x%) + gt%
      cy% = yt% + DIVMULT(yb% - yt%, gridscale%, y%) + gt%
      r% = DIVMULT(255, gridscale% - 1, x%)
      g% = DIVMULT(255, gridscale% - 1, y%)
      b% = DIVMULT(255, 2 * gridscale% - 2, 2 * gridscale% - 2 - x% - y%)
      RECOLOUR(cx%, cy%, (r% * &10000) + (g% * &100) + (b% * &1))
    NEXT
  NEXT
ENDPROC

DEF PROCmap(a%, x%, y%, RETURN newx%, RETURN newy%)
  newx% = DIVMULT(x%, coeffdiv%, mapxx%(a%)) + DIVMULT(y%, coeffdiv%, mapxy%(a%)) + mapxc%(a%)
  newy% = DIVMULT(x%, coeffdiv%, mapyx%(a%)) + DIVMULT(y%, coeffdiv%, mapyy%(a%)) + mapyc%(a%)
ENDPROC

DEF PROCdomap(mapnum%)
  LOCAL x%, y%, newx%, newy%
  NOUPDATE
  FOR y% = yt% TO yb%
    FOR x% = xl% TO xr%
      PROCmap(mapnum%, x%, y%, newx%, newy%)
      PIXSET(newx%, newy%, PIXCOL(x%, y%))
    NEXT
  NEXT
  UPDATE
ENDPROC
