REM GravitySteps

PROCsteps(10)
SETXY(950, 75)
REPEAT
  PROCthrowball(-2, -22, 1, turty% + 100)
UNTIL turtx% < 75
FOR bounce% = 1 TO 10
  PROCthrowball(0, bounce% * 2 - 20, 1, turty%)
NEXT
END

DEF PROCsteps(s%)
  LOCAL size%, count%
  size% = 1000 / s%
  SETXY(0, 1000)
  THICKNESS(1)
  FOR count% = 1 TO s% * 2
    IF count% MOD 2 = 0 THEN MOVEXY(0, -size%) ELSE MOVEXY(size%, 0)
  NEXT
  MOVEXY(0, s% * size%)
  MOVEXY(-s% * size%, 0)
  COLOUR(BLUE)
  POLYGON(s% * 2 + 2)
ENDPROC

DEF PROCthrowball(xvel%, yvel%, gravity%, floor%)
  REPEAT
    COLOUR(WHITE)
    BLOT(25)
    MOVEXY(xvel%, yvel%)
    yvel% = yvel% + gravity%
    IF turty% > floor% THEN turty% = floor%
    COLOUR(RED)
    BLOT(24)
    UPDATE
    PAUSE(10)
    NOUPDATE
  UNTIL turty% = floor%
ENDPROC
