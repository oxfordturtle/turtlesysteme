REM QuickClick

BLANK(CREAM)
SETXY(15, 200)
PRINT("This reaction game will display a sequence", 4, 36)
SETXY(20, 270)
PRINT("of 10 coloured circles on the Canvas. Try", 4, 36)
SETXY(20, 340)
PRINT("to click the mouse on each circle as quickly", 4, 36)
SETXY(20, 410)
PRINT("as you can, and see how short a total time", 4, 36)
SETXY(20, 480)
PRINT("you can achieve for all 10.", 4, 36)
SETXY(100, 620)
PRINT("Now press the ""Esc"" key to start ...", 4, 36)
KEYECHO(FALSE)
REPEAT
UNTIL (ABS(?KEY) = \ESCAPE)
TIMESET(0)
FOR count% = 1 TO 10
  x% = RND(900) + 50
  y% = RND(900) + 50
  BLANK(BLACK)
  SETXY(x%, y%)
  COLOUR(RGB(count%))
  BLOT(50)
  REPEAT
    REPEAT
      UPDATE
    UNTIL ?LMOUSE > 0
    clickcol% = PIXCOL(?MOUSEX, ?MOUSEY)
  UNTIL clickcol% = turtc%
NEXT
BLANK(LIGHTBLUE)
SETXY(60, 460)
COLOUR(LIGHTRED)
PRINT("Your time was " + QSTR$(TIME, 1000, 2) + " seconds", 4, 50)
END
