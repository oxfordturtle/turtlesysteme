REM IFSDemonstrator

CONST MAXMAPPINGS% = 4
CONST PAUSEVAL% = 2000
DIM mapxx%(MAXMAPPINGS%)
DIM mapxy%(MAXMAPPINGS%)
DIM mapxc%(MAXMAPPINGS%)
DIM mapyx%(MAXMAPPINGS%)
DIM mapyy%(MAXMAPPINGS%)
DIM mapyc%(MAXMAPPINGS%)
PROCstartprompt
PROCsetscale
CANVAS(xl%, yt%, xr% - xl% + 1, yb% - yt% + 1)
RESOLUTION(xr% - xl% + 1, yb% - yt% + 1)
IF seedpixel% THEN
  PIXSET(seedx%, seedy%, live%)
ELSE
  BLANK(live%)
ENDIF
iteration% = 0
TIMESET(0)
REPEAT
  starttime% = TIME
  INC(iteration%)
  WRITELN("")
  WRITELN("Iteration " + STR$(iteration%) + ":")
  numborn% = 0
  FOR count% = 1 TO mappings%
    PROCdomap(count%)
  NEXT
  numkilled% = 0
  PROCcleanup
  WRITELN("  " + STR$(numborn%) + " pixels born " + STR$(numkilled%) + " pixels killed")
  WRITELN("  (took " + STR$((TIME - starttime%) / 1000) + " seconds - total " + STR$(TIME / 1000) + " seconds so far)")
  IF (numborn% + numkilled% > 0) AND (PAUSEVAL% >= 1000) THEN
    WRITE("  (now pausing " + QSTR$(PAUSEVAL%, 1000, 1) + " seconds to allow halting if desired ...)")
    PAUSE(PAUSEVAL%)
    WRITELN("")
  ENDIF
UNTIL numborn% + numkilled% = 0
WRITELN("FINISH - fixed point of iterative mapping has been reached")
FOR count% = 1 TO mappings%
  PROCdomap(count%)
NEXT
END

DEF PROCsetsierpinski
  xleft% = 0
  xright% = 999
  ytop% = 0
  ybottom% = 999
  scaledown% = 2
  mappings% = 3
  live% = BLACK
  copied% = DARKGREY
  seedx% = 250
  seedy% = 0
  coeffdiv% = 100
  mapxx%(1) = 50
  mapyx%(1) = 0
  mapxy%(1) = 0
  mapyy%(1) = 50
  mapxc%(1) = 250
  mapyc%(1) = 0
  mapxx%(2) = 50
  mapyx%(2) = 0
  mapxy%(2) = 0
  mapyy%(2) = 50
  mapxc%(2) = 0
  mapyc%(2) = 500
  mapxx%(3) = 50
  mapyx%(3) = 0
  mapxy%(3) = 0
  mapyy%(3) = 50
  mapxc%(3) = 500
  mapyc%(3) = 500
ENDPROC

DEF PROCsetbarnsley
  xleft% = -236
  xright% = 263
  ytop% = 1
  ybottom% = 1000
  scaledown% = 2
  mappings% = 4
  live% = SEAGREEN
  copied% = EMERALD
  seedx% = 1
  seedy% = 1
  coeffdiv% = 100
  mapxx%(1) = 0
  mapyx%(1) = 0
  mapxy%(1) = 0
  mapyy%(1) = 16
  mapxc%(1) = 0
  mapyc%(1) = 0
  mapxx%(2) = 85
  mapyx%(2) = -4
  mapxy%(2) = 4
  mapyy%(2) = 85
  mapxc%(2) = 0
  mapyc%(2) = 160
  mapxx%(3) = 20
  mapyx%(3) = 23
  mapxy%(3) = -26
  mapyy%(3) = 22
  mapxc%(3) = 0
  mapyc%(3) = 160
  mapxx%(4) = -15
  mapyx%(4) = 26
  mapxy%(4) = 28
  mapyy%(4) = 24
  mapxc%(4) = 0
  mapyc%(4) = 44
ENDPROC

DEF PROCsetdragon
  xleft% = 0
  xright% = 999
  ytop% = 0
  ybottom% = 999
  scaledown% = 4
  mappings% = 3
  live% = BLACK
  copied% = SILVER
  seedx% = 1
  seedy% = 1
  coeffdiv% = 1000
  mapxx%(1) = 0
  mapyx%(1) = -577
  mapxy%(1) = 577
  mapyy%(1) = 0
  mapxc%(1) = 95
  mapyc%(1) = 589
  mapxx%(2) = 0
  mapyx%(2) = -577
  mapxy%(2) = 577
  mapyy%(2) = 0
  mapxc%(2) = 441
  mapyc%(2) = 789
  mapxx%(3) = 0
  mapyx%(3) = -577
  mapxy%(3) = 577
  mapyy%(3) = 0
  mapxc%(3) = 95
  mapyc%(3) = 989
ENDPROC

DEF PROCstartprompt
  OUTPUT(TRUE, CREAM, TRUE)
  KEYECHO(FALSE)
  WRITELN("ITERATED FUNCTION SYSTEMS DEMONSTRATOR PROGRAM")
  WRITELN("")
  WRITE("Select Barnsley fern, Sierpinski, or Dragon curve? (B/S/D) ")
  KEYECHO(FALSE)
  REPEAT
  UNTIL (ABS(?KEY) = \b) OR (ABS(?KEY) = \s) OR (ABS(?KEY) = \d)
  WRITELN(CHR$(ABS(?KEY)))
  IF ABS(?KEY) = \b THEN
    WRITELN("Barnsley fern mappings will be used")
    PROCsetbarnsley
  ELSE
    IF ABS(?KEY) = \s THEN
      WRITELN("Sierpinski mappings will be used")
      PROCsetsierpinski
    ELSE
      WRITELN("Dragon curve mappings will be used")
      PROCsetdragon
    ENDIF
  ENDIF
  WRITELN("")
  WRITE("Start with single Pixel or entire Canvas? (P/C) ")
  KEYECHO(FALSE)
  REPEAT
  UNTIL (ABS(?KEY) = \p) OR (ABS(?KEY) = \c)
  WRITELN(CHR$(ABS(?KEY)))
  seedpixel% = (ABS(?KEY) = \p)
  WRITELN("")
  WRITE("Use Uniform or Different colours for mappings? (U/D) ")
  KEYECHO(FALSE)
  REPEAT
  UNTIL (ABS(?KEY) = \u) OR (ABS(?KEY) = \d)
  WRITELN(CHR$(ABS(?KEY)))
  uniform% = (ABS(?KEY) = \u)
  OUTPUT(FALSE, WHITE, FALSE)
ENDPROC

DEF PROCsetscale
LOCAL i%
  xl% = xleft% / scaledown%
  xr% = xright% / scaledown%
  yt% = ytop% / scaledown%
  yb% = ybottom% / scaledown%
  FOR i% = 1 TO mappings%
    mapxc%(i%) = mapxc%(i%) / scaledown%
    mapyc%(i%) = mapyc%(i%) / scaledown%
  NEXT
ENDPROC

DEF PROCmap(a%, x%, y%, RETURN newx%, RETURN newy%)
  newx% = DIVMULT(x%, coeffdiv%, mapxx%(a%)) + DIVMULT(y%, coeffdiv%, mapxy%(a%)) + mapxc%(a%)
  newy% = DIVMULT(x%, coeffdiv%, mapyx%(a%)) + DIVMULT(y%, coeffdiv%, mapyy%(a%)) + mapyc%(a%)
ENDPROC

DEF PROCdomap(mapnum%)
LOCAL x%, y%, newx%, newy%
  NOUPDATE
  FOR y% = yt% TO yb%
    FOR x% = xl% TO xr%
      IF PIXCOL(x%, y%) <> WHITE THEN
        PROCmap(mapnum%, x%, y%, newx%, newy%)
        IF PIXCOL(newx%, newy%) = WHITE THEN INC(numborn%)
        IF uniform% THEN
          PIXSET(newx%, newy%, copied%)
        ELSE
          PIXSET(newx%, newy%, RGB(mapnum%))
        ENDIF
      ENDIF
    NEXT
  NEXT
  UPDATE
ENDPROC

DEF PROCcleanup
LOCAL x%, y%
  NOUPDATE
  FOR y% = yt% TO yb%
    FOR x% = xl% TO xr%
      IF PIXCOL(x%, y%) = live% THEN
        PIXSET(x%, y%, WHITE)
        INC(numkilled%)
      ELSE
        IF PIXCOL(x%, y%) <> WHITE THEN
          PIXSET(x%, y%, live%)
        ENDIF
      ENDIF
    NEXT
  NEXT
  UPDATE
ENDPROC
