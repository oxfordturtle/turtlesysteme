REM RecursionFactory

NUMSIDES%=6
INITSIZE%=240
SHRINK%=40
RANGLE%=180
POLYGAP%=0
LEVELS%=5
SLOWDRAW%=3
MODE%=1
FIRSTCOL%=1
COLINC%=2
MINTHICK%=2
ADDTHICK%=6
CIRCSIZE%=60
BLANK(BLACK)
IF 180 MOD NUMSIDES% = 0 THEN
  halfangle% = 90 - (180 / NUMSIDES%)
  slowlev% = LEVELS% + 1 - SLOWDRAW%
  IF slowlev% > levels - 1 THEN slowlev% = LEVELS% - 1
  MOVEXY(0, INITSIZE%)
  PROCrecurse(1, INITSIZE%, LEVELS%, FIRSTCOL%)
ENDIF
END

DEF PROCrecurse(flag%, size%, depth%, col%)
  IF depth% > 0 THEN
    IF depth% < slowlev% THEN NOUPDATE ELSE UPDATE
    IF flag% = 1 THEN
      FOR flag% = 2 TO 3
        storex% = TURTX%
        storey% = TURTY%
        FOR sides% = 1 TO NUMSIDES%
          PROCrecurse(flag%, size%, depth%, col%)
        NEXT
        TURTX% = storex%
        TURTY% = storey%
      NEXT
    ELSE
      PENUP
      IF flag% = 2 THEN
        COLOUR(RGB(col%))
        THICKNESS(MINTHICK% + ((depth% - 1) * ADDTHICK%) / 12)
        IF MODE% = 1 THEN
          PENDOWN
        ELSE
          IF MODE% = 2 THEN
            BLOT(size% * CIRCSIZE% / 120)
          else
            CIRCLE(size% * CIRCSIZE% / 120)
          ENDIF
        ENDIF
      ENDIF
      LEFT(halfangle%)
      FORWARD(size%)
      RIGHT(180 - halfangle% + RANGLE%)
      IF flag% = 3 THEN
        FORWARD(size% * POLYGAP% / 120)
        PROCrecurse(1, (size% * SHRINK%) / 120, depth% - 1, ((col% + COLINC% - 1) MOD 7) + 1)
        BACK(size% * POLYGAP% / 120)
      ENDIF
      LEFT(RANGLE%)
    ENDIF
  ENDIF
ENDPROC
