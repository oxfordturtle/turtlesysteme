REM BouncingShapes

CONST MINSIZE% = 70
CONST MAXSIZE% = 130
CONST SHAPES% = 6
DIM x%(SHAPES%)
DIM y%(SHAPES%)
DIM d%(SHAPES%)
DIM xvel%(SHAPES%)
DIM yvel%(SHAPES%)
DIM dvel%(SHAPES%)
DIM sides%(SHAPES%)
DIM size%(SHAPES%)
DIM colr%(SHAPES%)
FOR n% = 1 TO SHAPES%
  sides%(n%) = n% + 2
  size%(n%) = MINSIZE% + RND(MAXSIZE% - MINSIZE%)
  colr%(n%) = RGB(n%)
  x%(n%) = RND(1000 - 2 * size%(n%)) + size%(n%)
  y%(n%) = RND(1000 - 2 * size%(n%)) + size%(n%)
  d%(n%) = RND(360)
  xvel%(n%) = RND(15) - 7
  yvel%(n%) = RND(15) - 7
  dvel%(n%) = RND(4) + 3
NEXT
WHILE 0 < 1
  NOUPDATE
  BLANK(WHITE)
  FOR n% = 1 TO SHAPES%
    x%(n%) = x%(n%) + xvel%(n%)
    y%(n%) = y%(n%) + yvel%(n%)
    d%(n%) = (d%(n%) + dvel%(n%)) MOD 360
    SETXY(x%(n%), y%(n%))
    DIRECTION(d%(n%))
    PROCdrawshape(sides%(n%), size%(n%), colr%(n%))
    IF bouncex% THEN
      xvel%(n%) = -xvel%(n%)
      dvel%(n%) = -dvel%(n%)
    ENDIF
    IF bouncey% THEN
      yvel%(n%) = -yvel%(n%)
      dvel%(n%) = -dvel%(n%)
    ENDIF
  NEXT
  UPDATE
  PAUSE(5)
ENDWHILE
END

DEF PROCdrawshape(sid%, siz%, col%)
  LOCAL count%
  bouncex% = FALSE
  bouncey% = FALSE
  COLOUR(col%)
  FOR count% = 1 TO sid%
    FORWARD(siz%)
    IF ABS(turtx% - 500) > 495 THEN
      bouncex% = TRUE
    ENDIF
    IF ABS(turty% - 500) > 495 THEN
      bouncey% = TRUE
    ENDIF
    BACK(siz%)
    FORGET(1)
    RIGHT(360 / sid%)
  NEXT
  POLYGON(sid%)
ENDPROC
