REM Schelling

CONST WIDTH% = 50
CONST HEIGHT% = 50
CONST EMPTY% = GREEN
CANVAS(-1, -1, WIDTH% + 2, HEIGHT% + 2)
RESOLUTION(WIDTH% + 2, HEIGHT% + 2)
NOUPDATE
BLANK(EMPTY%)
FOR i% = 0 TO WIDTH% - 1
  FOR j% = 0 TO WIDTH% - 1
    test% = RND(25)
    IF test% = 1 THEN
      PIXSET(i%, j%, EMPTY%)
    ELSE
      IF (i% + j%) MOD 2 = 0 THEN PIXSET(i%, j%, RED) ELSE PIXSET(i%, j%, BLUE)
    ENDIF
  NEXT
NEXT
PAUSE(2000)
WHILE ?KEY <> \ESCAPE
  NOUPDATE
  REPEAT
    tryi% = RND(WIDTH%) - 1
    tryj% = RND(HEIGHT%) - 1
    this% = PIXCOL(tryi%, tryj%)
  UNTIL (this% <> EMPTY%) AND (NOT(FNhappy(tryi%, tryj%, this%)))
  PIXSET(tryi%, tryj%, EMPTY%)
  REPEAT
    tryi% = RND(WIDTH%) - 1
    tryj% = RND(HEIGHT%) - 1
  UNTIL (PIXCOL(tryi%, tryj%) = EMPTY%) AND FNhappy(tryi%, tryj%, this%)
  PIXSET(tryi%, tryj%, this%)
  UPDATE
ENDWHILE
END

DEF FNhappy(x%, y%, c%)
  LOCAL like%, unlike%, neighbour%, i%, j%
  like% = 0
  unlike% = 0
  FOR i% = -1 TO 1
    FOR j% = -1 TO 1
      IF (i% <> 0) OR (j% <> 0) THEN
        neighbour% = PIXCOL(x% + i%, y% + j%)
        IF neighbour% <> EMPTY% THEN
          IF neighbour% = c% THEN INC(like%) ELSE INC(unlike%)
        ENDIF
      ENDIF
    NEXT
  NEXT
= (like% >= unlike% - 1)
