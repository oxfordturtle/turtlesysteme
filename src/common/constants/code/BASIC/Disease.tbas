REM Disease

CONST WIDTH% = 100
CONST HEIGHT% = 100
CONST SUSCEPTIBLE% = LIGHTGREEN
CONST INFECTED% = RED
CONST RECOVERED% = BLUE
CONST STARTRADIUS% = 10
CONST INFECTPROB% = 1
CONST IMMUNEPROB% = 2
CONST RECOVERPROB% = 15
CANVAS(0, 0, WIDTH%, HEIGHT%)
RESOLUTION(WIDTH%, HEIGHT%)
numinfected% = 0
NOUPDATE
FOR x% = 0 TO WIDTH% - 1
  FOR y% = 0 TO HEIGHT% - 1
    IF (RND(100) - 1 < INFECTPROB%) AND (HYPOT(x% - WIDTH% / 2, y% - HEIGHT% / 2, 1) <= STARTRADIUS%) THEN
      PROCinfect(x%, y%)
    ELSE
      IF RND(100) - 1 < IMMUNEPROB% THEN
        PIXSET(x%, y%, RECOVERED%)
      ELSE
        PIXSET(x%, y%, SUSCEPTIBLE%)
      ENDIF
    ENDIF
  NEXT
NEXT
UPDATE
REPEAT
  x% = RND(WIDTH%) - 1
  y% = RND(HEIGHT%) - 1
  IF PIXCOL(x%, y%) = INFECTED% THEN
    IF RND(100) - 1 < RECOVERPROB% THEN
      PIXSET(x%, y%, RECOVERED%)
      DEC(numinfected%)
    ELSE
      n% = (RND(4) - 1) * 2 + 1
      x% = x% + n% DIV 3 - 1
      y% = y% + n% MOD 3 - 1
      IF PIXCOL(x%, y%) = SUSCEPTIBLE% THEN PROCinfect(x%, y%)
    ENDIF
  ENDIF
UNTIL numinfected% = 0
END

DEF PROCinfect(x%, y%)
  PIXSET(x%, y%, INFECTED%)
  INC(numinfected%)
ENDPROC
